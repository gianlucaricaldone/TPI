// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © davidriogita

//@version=5
indicator(title="TPI", shorttitle= "TPI Alert", overlay=false, max_bars_back = 21)

// 1 - AROON
aroon_signal() =>
    length = input.int(14, minval=1)
    upper = 100 * (ta.highestbars(high, length+1) + length)/length
    lower = 100 * (ta.lowestbars(low, length+1) + length)/length
    if upper == lower
        valore = 0
    else
        valore = upper > lower? 1:-1


// 2 - DMI
dmi_signal() =>
    len = input.int(14, minval=1, title="DI Length")
    lensig = input.int(14, title="ADX Smoothing", minval=1, maxval=50)
    [diplus, diminus, adx] = ta.dmi(len, lensig)
    if diplus == diminus
        valore = 0
    else
        valore = diplus > diminus? 1:-1


// 3 - MACD Signal
macd_signal() =>
    fast = 12
    slow = 26
    fastMA = ta.ema(close, fast)
    slowMA = ta.ema(close, slow)
    macd = fastMA - slowMA
    signal = ta.ema(macd, 9)
    if macd == signal
        valore = 0
    else
        valore = macd > signal? 1:-1


// 4 - Parabolic SAR
sar_signal() =>
    start = input.float(title="Start", defval=0.02, step=0.01)
    inc = input.float(title="Inc", defval=0.01, step=0.01)
    max = input.float(title="Max", defval=0.2, step=0.01)

    PSAR = ta.sar(start, inc, max)
    if PSAR == low
        valore = 0
    else
        valore = PSAR < low? 1:-1


// 5 - RSI
rsi_signal() =>
    rsi = ta.rsi(close, 14)
    valore = rsi > 50? 1:-1


// 6 - SUPERTREND
supertrend_signal() =>
    [supertrend, direction] = ta.supertrend(3, 10)
    if direction == 0
        valore = 0
    else
        valore = direction < 0? 1:-1


// 7 - SMI
smi_signal() =>
    shortlen = input.int(5, minval=1, title="Short Period")
    longlen = input.int(20, minval=1, title="Long Period")
    siglen = input.int(5, minval=1, title="Signal Line Period)")
    erg = ta.tsi(close, shortlen, longlen)
    sig = ta.ema(erg, siglen)
    osc = erg - sig
    valore = osc > 0? 1:-1


// Valori singoli indicatori TPI (0/1)
aroon_value = aroon_signal()
dmi_value = dmi_signal()
macd_value = macd_signal()
sar_value = sar_signal()
rsi_value = rsi_signal()
supertrend_value = supertrend_signal()
smi_value = smi_signal()

TPI_total = (aroon_value + dmi_value + macd_value + sar_value + rsi_value + supertrend_value + smi_value) / 7

// Grafica
TPI_buy_coeff = 0.5
TPI_sell_coeff = -0.5
transparency = 75
plot_color = color.gray
plt_color_ts = color.new(color.gray, transparency)
if TPI_total >= TPI_buy_coeff
    plot_color := color.green
    plt_color_ts := color.new(color.green, transparency)
else if TPI_total <= TPI_sell_coeff
    plot_color := color.red
    plt_color_ts := color.new(color.red, transparency)

// Grafica
hline(0, title = "0", color = color.gray, linestyle = hline.style_dotted, linewidth = 1)
hline(TPI_buy_coeff, title = "BUY", color = color.green, linestyle = hline.style_dotted, linewidth = 1)
hline(TPI_sell_coeff, title = "SELL", color = color.red, linestyle = hline.style_dotted, linewidth = 1)
plot(TPI_total, color = plot_color, linewidth = 2)
plot(TPI_total, color = plt_color_ts, linewidth = 2, style=plot.style_area)